plugins {
    id 'java'
    // Check for new versions at https://plugins.gradle.org/plugin/io.papermc.paperweight.userdev
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.14"
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'com.gradleup.shadow' version '9.0.0-beta7'
}

group = 'net.sabafly'
version = '0.6.0'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = 'https://jitpack.io'
    }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }
}

dependencies {
    implementation 'org.spongepowered:configurate-yaml:4.2.0-SNAPSHOT'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7.1"
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'com.github.Jikoo.OpenInv:openinvapi:5.1.4'
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    paperweight.paperDevBundle("1.21.1-R0.1-SNAPSHOT")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

//noinspection UnnecessaryQualifiedReference
tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    relocate('org.spongepowered.configurate', 'net.sabafly.configurate')
    relocate('io.leangen.geantyref', 'net.sabafly.geantyref')

    archiveBaseName = 'EmeraldBank'
    archiveClassifier = ''
    archiveVersion = "${version}"

    minimize()
}

tasks.build {
    dependsOn shadowJar
}

tasks {
    runServer {
        //noinspection GroovyMissingReturnStatement
        downloadPlugins {
            github("Jikoo", "OpenInv", "5.1.6", "OpenInv.jar")
            github("MilkBowl", "Vault", "1.7.3", "Vault.jar")
            url("https://download.luckperms.net/1570/bukkit/loader/LuckPerms-Bukkit-5.4.153.jar")
            url("https://ci.extendedclip.com/job/PlaceholderAPI/lastSuccessfulBuild/artifact/build/libs/PlaceholderAPI-2.11.7-DEV-200.jar")
        }
        minecraftVersion("1.21.4")
    }
}
