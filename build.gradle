plugins {
    id 'java'
    // Check for new versions at https://plugins.gradle.org/plugin/io.papermc.paperweight.userdev
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.19"
    id("xyz.jpenilla.run-paper") version "3.0.1"
    id 'com.gradleup.shadow' version '9.2.2'
}

group = 'net.sabafly'
version = '1.1.2-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url = 'https://jitpack.io'
    }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }
    maven {
        name = "essentialsxReleases"
        url = "https://repo.essentialsx.net/releases"
    }
}

dependencies {
    compileOnly("com.h2database:h2:2.4.240")
    compileOnly("com.mysql:mysql-connector-j:9.4.0")
    implementation("com.zaxxer:HikariCP:7.0.2")
    implementation("commons-dbutils:commons-dbutils:1.8.1")
    implementation 'org.spongepowered:configurate-yaml:4.3.0-SNAPSHOT'
    implementation("com.vdurmont:semver4j:3.1.0")

    compileOnly("com.github.MilkBowl:VaultAPI:1.7.1") {
        transitive = false
    }
    compileOnly('me.clip:placeholderapi:2.11.6') {
        transitive = false
    }
    compileOnly('com.github.Jikoo:OpenInv:5.1.14')
    compileOnly("net.essentialsx:EssentialsX:2.21.2") {
        exclude(group: 'org.spigotmc', module: 'spigot-api')
        transitive = false
    }

    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    paperweight.paperDevBundle("1.21.10-R0.1-SNAPSHOT")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

//noinspection UnnecessaryQualifiedReference
tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {

    archiveBaseName = 'EmeraldBank'
    archiveClassifier = ''
    archiveVersion = "${version}"

    dependencies {
        exclude(dependency("org.slf4j:slf4j-api"))
        exclude(dependency("net.kyori:option"))
    }

    minimize()

    relocate('org.spongepowered.configurate', 'net.sabafly.configurate')
    relocate('io.leangen.geantyref', 'net.sabafly.geantyref')

    relocate("com.zaxxer.hikari", "net.sabafly.libs.com.zaxxer.hikari")
    relocate("org.apache.commons.dbutils", "net.sabafly.libs.org.apache.commons.dbutils")

    relocate("com.vdurmont.semver4j", "net.sabafly.libs.com.vdurmont.semver4j")
}

tasks.build {
    dependsOn shadowJar
}

tasks {
    runServer {
        //noinspection GroovyMissingReturnStatement
        downloadPlugins {
            github("Jikoo", "OpenInv", "5.1.15", "OpenInv.jar")
            github("MilkBowl", "Vault", "1.7.3", "Vault.jar")
            url("https://ci.ender.zone/job/EssentialsX/1734/artifact/jars/EssentialsX-2.22.0-dev+39-adf77dc.jar")
            modrinth("luckperms", "v5.5.17-bukkit")
            url("https://ci.extendedclip.com/job/PlaceholderAPI/208/artifact/build/libs/PlaceholderAPI-2.11.7-DEV-208.jar")
        }
        minecraftVersion("1.21.10")
    }
}
